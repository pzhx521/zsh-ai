#!/usr/bin/env zsh

# OpenAI API provider for zsh-ai

_zsh_ai_openai_supports_temperature() {
    local model="$1"

    # These OpenAI reasoning models reject non-default temperature values.
    [[ "$model" != gpt-5* && "$model" != o1* && "$model" != o3* && "$model" != o4* ]]
}

# Function to call OpenAI API
_zsh_ai_query_openai() {
    local query="$1"
    local response
    
    # Build context
    local context=$(_zsh_ai_build_context)
    local escaped_context=$(_zsh_ai_escape_json "$context")
    local system_prompt=$(_zsh_ai_get_system_prompt "$escaped_context")
    local escaped_system_prompt=$(_zsh_ai_escape_json "$system_prompt")
    
    # Prepare the JSON payload - escape quotes in the query
    local escaped_query=$(_zsh_ai_escape_json "$query")

    # Determine token parameter based on model (newer models use max_completion_tokens)
    local token_param="max_completion_tokens"
    if [[ "$ZSH_AI_OPENAI_MODEL" == gpt-4* ]] || [[ "$ZSH_AI_OPENAI_MODEL" == gpt-3.5* ]]; then
        token_param="max_tokens"
    fi

    local temperature_param=""
    if _zsh_ai_openai_supports_temperature "$ZSH_AI_OPENAI_MODEL"; then
        temperature_param=$',\n    "temperature": 0.3'
    fi

    local json_payload=$(cat <<EOF
{
    "model": "${ZSH_AI_OPENAI_MODEL}",
    "messages": [
        {
            "role": "system",
            "content": "$escaped_system_prompt"
        },
        {
            "role": "user",
            "content": "$escaped_query"
        }
    ],
    "$token_param": 256${temperature_param}
}
EOF
)
    
    # Call the API - only add auth header if API key is set
    # ZSH_AI_OPENAI_API_KEY takes precedence (useful for LiteLLM and other proxies)
    local auth_args=()
    local api_key="${ZSH_AI_OPENAI_API_KEY:-$OPENAI_API_KEY}"
    [[ -n "$api_key" ]] && auth_args=(--header "Authorization: Bearer $api_key")

    _zsh_ai_curl "${ZSH_AI_OPENAI_URL}" "$json_payload" "${auth_args[@]}"

    if [[ $? -ne 0 ]]; then
        _zsh_ai_error_report "Error: Failed to connect to OpenAI API"
        return 1
    fi
    response="$ZSH_AI_LAST_RESPONSE"
    
    # Debug: Uncomment to see raw response
    # echo "DEBUG: Raw response: $response" >&2
    
    # Extract the content from the response
    # Try using jq if available, otherwise fall back to sed/grep
    if command -v jq &> /dev/null; then
        local result=$(printf "%s" "$response" | jq -r '.choices[0].message.content // empty' 2>/dev/null)
        if [[ -z "$result" ]]; then
            # Check for error message
            local error=$(printf "%s" "$response" | jq -r '.error.message // empty' 2>/dev/null)
            if [[ -n "$error" ]]; then
                _zsh_ai_error_report "API Error: $error"
            else
                _zsh_ai_error_report "Error: Unable to parse response"
            fi
            return 1
        fi
        # Clean up the response - remove newlines and trailing whitespace
        # Commands should be single-line for shell execution
        result=$(printf "%s" "$result" | tr -d '\n' | sed 's/[[:space:]]*$//')
        printf "%s" "$result"
    else
        # Fallback parsing without jq - handle responses with newlines
        # Use sed to extract the content field, handling potential newlines
        local result=$(printf "%s" "$response" | sed -n 's/.*"content":"\([^"]*\)".*/\1/p' | head -1)

        # If the simple extraction failed, try a more complex approach for multiline responses
        if [[ -z "$result" ]]; then
            # Extract content field even if it contains escaped newlines
            result=$(printf "%s" "$response" | perl -0777 -ne 'print $1 if /"content":"((?:[^"\\]|\\.)*)"/s' 2>/dev/null)
        fi

        if [[ -z "$result" ]]; then
            _zsh_ai_error_report "Error: Unable to parse response (install jq for better reliability)"
            return 1
        fi

        # Unescape JSON string (handle \n, \t, etc.) and clean up
        result=$(printf "%s" "$result" | sed 's/\\n/\n/g; s/\\t/\t/g; s/\\r/\r/g; s/\\"/"/g; s/\\\\/\\/g')
        # Remove trailing newlines and spaces
        result=$(printf "%s" "$result" | sed 's/[[:space:]]*$//')
        printf "%s" "$result"
    fi
}
